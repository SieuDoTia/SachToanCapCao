# cd ../../../Applications
# cd ./gnuplot.app/Contents/macos/
# ./gnuplot.sh
# Hình 2_3_1

reset

# Đặt SVG
set terminal svg size 200, 200
set output '2_3_1.svg'

#set title '????' font 'TimesNewRoman, 10'


# ==== Những Nhẵn
# tên hàm
set label 'y(x)' font 'TimesNewRoman, 10' at 4,5.5  front textcolor "#000000"  left


# tên điểm
set label  'A' font 'TimesNewRoman, 8' at 1.1,-0.3  front textcolor "#000000" left
set label  'B' font 'TimesNewRoman, 8' at 3.2,-0.3  front textcolor "#000000" left

set label  '∆x' font 'TimesNewRoman, 8' at 2.15,-0.8  front textcolor "#000000" left
set label  '∆y' font 'TimesNewRoman, 8' at 3.9,3.65  front textcolor "#000000" left


#–

# ===== Đồ Thị
set style line 10 linecolor rgb '#80eeff' linetype 1 linewidth 0.5
set grid back ls 10

# ==== Trục
# Phạm vi trục
set xrange[-2:6]
set yrange[-2:6]

# Tên trục
set label 'y' font 'TimesNewRoman, 10' at first 0, graph 1 offset -1,0.5 center
set label 'x' font 'TimesNewRoman, 10' at graph 1, first 0 offset 1,-0.5 rotate by 1.5708 center

# ==== Bước trục
set border 0 back
# set tics nomirror 
#set zeroaxis
set xtics 1
set ytics 1
set tics scale 0.0
set format x ""
set format y ""
#unset xtics
#unset ytics

set style line 4 linecolor rgb '#4500d3' linetype 0 linewidth 0.8


# ==== Mũi tên kiểu 3 cho trục
set arrow 3 from graph 0.0,0.25 to graph 1.05,0.25 size screen 0.025,15,60 \
    filled ls 4 lt 1 lc '#9400d3'
set arrow from graph 0.25,0 to graph 0.25,1.05 size screen 0.025,15,60 \
    filled ls 4 lt 1 lc '#4500d3'

# Phong Cách Điểm
set pointsize 1.5
set style line 5 lc rgb '#000000' pt 5   # square
set style line 6 lc rgb '#000000' pt 7   # circle
set style line 7 lc rgb '#0060ad' pt 9   # triangle

# Điểm
k = 1.5  # phóng to hàm

xd = -4.1;  yd = -1.2   # nới/dịch 
mu = 0.55
b = 2

x0 = 1.1;    y0 = k*(b*x0 - xd)**mu + yd
x1 = 3.2;    y1 = k*(b*x1 - xd)**mu + yd

# ==== cho vẽ nét
set arrow from x0,0 to x0,y0 nohead lw 0.6 lt 0 lc '#000080'
set arrow from x1,0 to x1,y1 nohead lw 0.6 lt 0 lc '#000080'

set arrow from x0,y0 to x1,y0 nohead lw 0.6 lt 0 lc '#000080'
#set arrow from 0,y1 to x1,y1 nohead lw 0.6 lt 0 lc '#000080'



# ==== Vẽ Hàm
# Danh Sách Hàm
set style line 1 linecolor rgb '#0000ff' linetype 1 linewidth 0.8
set style line 2 linecolor rgb '#007fff' linetype 1 linewidth 0.8
set style line 3 linecolor rgb '#ff007f' linetype 1 linewidth 0.8

# ==== 
f(x) = k*(b*x - xd)**mu + yd


# Vẽ
plot  f(x) notitle with lines ls 1
