# cd ../../../Applications
# cd ./gnuplot.app/Contents/macos/
# ./gnuplot.sh
# Hình 1_4_03

reset

# Đặt SVG
set terminal svg size 200, 200
set output '1_4_03.svg'

#set title '????' font 'TimesNewRoman, 10'


# ==== Những Nhẵn
# tên hàm
set label 'y = 1/x' font 'TimesNewRoman, 10' at 0.2,0.4  front textcolor "#000000"  left
set label 'y = 2/x' font 'TimesNewRoman, 10' at 0.8,3.3  front textcolor "#000000"  left
set label 'y = –1/(2x)' font 'TimesNewRoman, 10' at 0.5,-2  front textcolor "#000000"  left


# tên điểm
set label  'A = (x_0; y_0)' font 'TimesNewRoman, 8' at 2.1,1.2  front textcolor "#000000" left



#–

# ===== Đồ Thị
set style line 10 linecolor rgb '#80eeff' linetype 1 linewidth 0.5
set grid back ls 10

# ==== Trục
# Phạm vi trục
set xrange[-4:4]
set yrange[-4:4]

# Tên trục
set label 'y' font 'TimesNewRoman, 10' at first 0, graph 1 offset -1,0.5 center
set label 'x' font 'TimesNewRoman, 10' at graph 1, first 0 offset 1,-0.5 rotate by 1.5708 center

# ==== Bước trục
set border 0 back
# set tics nomirror 
#set zeroaxis
set xtics 1
set ytics 1
set tics scale 0.0
set format x ""
set format y ""
#unset xtics
#unset ytics

set style line 11 linecolor rgb '#4500d3' linetype 0 linewidth 0.8


# Mũi tên kiểu 3 cho trục
set arrow 3 from graph 0.0,0.5 to graph 1.05,0.5 size screen 0.025,15,60 \
    filled ls 11 lt 1 lc '#9400d3'
set arrow from graph 0.5,0 to graph 0.5,1.05 size screen 0.025,15,60 \
    filled ls 11 lt 1 lc '#4500d3'

# Phong Cách Điểm
set pointsize 1.5
set style line 5 lc rgb '#000000' pt 5   # square
set style line 6 lc rgb '#000000' pt 7   # circle
set style line 7 lc rgb '#0060ad' pt 9   # triangle

# Điểm
x0 = 2;   y0 = 0
x1 = 2;   y1 = 0.5
x2 = 2;   y2 = 1


# ==== cho vẽ nét
set arrow from x0,y0 to x2,y2 nohead lw 0.6 lt 0 lc '#000080'


# ==== Vẽ Hàm
# Danh Sách Hàm
set style line 1 linecolor rgb '#0000ff' linetype 1 linewidth 0.8
set style line 2 linecolor rgb '#8f008f' linetype 1 linewidth 0.8
set style line 3 linecolor rgb '#ff002f' linetype 1 linewidth 0.8


f(x) = 1/x
g(x) = 2/x
h(x) = -0.5/x

# Vẽ
plot f(x) notitle with lines ls 1, \
     g(x) notitle with lines ls 2, \
     h(x) notitle with lines ls 3, \
      '+' using (x0):(y0) notitle w p ls 6 ps 0.3 lc rgb '#000000', \
      '+' using (x1):(y1) notitle w p ls 6 ps 0.3 lc rgb '#000000', \
      '+' using (x2):(y2) notitle w p ls 6 ps 0.3 lc rgb '#000000'