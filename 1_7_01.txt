# cd ../../../Applications
# cd ./gnuplot.app/Contents/macos/
# ./gnuplot.sh
# Hình 1_7_01

reset

# Đặt SVG
set terminal svg size 200, 200
set output '1_7_01.svg'

#set title '????' font 'TimesNewRoman, 10'


# ==== Những Nhẵn
# tên hàm
set label 'y = f(x)' font 'TimesNewRoman, 10' at 0.5,3  front textcolor "#000000"  left
set label 'y = f(x) + b' font 'TimesNewRoman, 10' at 0.5,5.1  front textcolor "#000000"  left
set label 'y = f(x) – b' font 'TimesNewRoman, 10' at 0.5,1.0  front textcolor "#000000"  left


# tên điểm
set label  'x_0' font 'TimesNewRoman, 8' at 3,-.25  front textcolor "#000000" left
set label  'A = (x_0; y_0)' font 'TimesNewRoman, 8' at 3.25,2.9  front textcolor "#000000" left
set label  'A_1 = (x_0; y_1)' font 'TimesNewRoman, 8' at 3.25,5.0  front textcolor "#000000" left


#–

# ===== Đồ Thị
set style line 10 linecolor rgb '#80eeff' linetype 1 linewidth 0.5
set grid back ls 10

# ==== Trục
# Phạm vi trục
set xrange[-2:6]
set yrange[-1:7]

# Tên trục
set label 'y' font 'TimesNewRoman, 10' at first 0, graph 1 offset -1,0.5 center
set label 'x' font 'TimesNewRoman, 10' at graph 1, first 0 offset 1,-0.5 rotate by 1.5708 center

# ==== Bước trục
set border 0 back
# set tics nomirror 
#set zeroaxis
set xtics 1
set ytics 1
set tics scale 0.0
set format x ""
set format y ""
#unset xtics
#unset ytics

set style line 11 linecolor rgb '#4500d3' linetype 0 linewidth 0.8


# Mũi tên kiểu 3 cho trục
set arrow 3 from graph 0.0,0.125 to graph 1.05,0.125 size screen 0.025,15,60 \
    filled ls 11 lt 1 lc '#9400d3'
set arrow from graph 0.25,0 to graph 0.25,1.05 size screen 0.025,15,60 \
    filled ls 11 lt 1 lc '#4500d3'

# Phong Cách Điểm
set pointsize 1.5
set style line 15 lc rgb '#000000' pt 5   # square
set style line 16 lc rgb '#000000' pt 7   # circle
set style line 17 lc rgb '#0060ad' pt 9   # triangle

# Điểm
x0 = 3.1;    y0 = 0.5*sinh(0.4*x0-1.2) + 1
x1 = 3.1;    y1 = 0.5*sinh(0.4*x1-1.2) + 3.1
x2 = 3.1;    y2 = 0.5*sinh(0.4*x2-1.2) + 5.2


# ==== cho vẽ nét
set arrow from x0,0 to x2,y2 nohead lw 0.6 lt 0 lc '#000080'
#set arrow from 0,y0 to x0,y0 nohead lw 0.6 lt 0 lc '#000080'


# ==== Vẽ Hàm
# Danh Sách Hàm
set style line 1 linecolor rgb '#0000ff' linetype 1 linewidth 0.8
set style line 2 linecolor rgb '#004fff' linetype 1 linewidth 0.8
set style line 3 linecolor rgb '#007fff' linetype 1 linewidth 0.8


f(x) = 0.5*sinh(0.4*x-1.2) + 1
g(x) = 0.5*sinh(0.4*x-1.2) + 3.1
h(x) = 0.5*sinh(0.4*x-1.2) + 5.2


# Vẽ
plot f(x) notitle with lines ls 1, \
     g(x) notitle with lines ls 2, \
     h(x) notitle with lines ls 3, \
      '+' using (x0):(0) notitle w p ls 16 ps 0.3, \
      '+' using (x1):(y1) notitle w p ls 16 ps 0.3, \
      '+' using (x2):(y2) notitle w p ls 16 ps 0.3
